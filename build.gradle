plugins {
	id 'java'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id 'com.diffplug.spotless' version "8.0.0"
}

group = 'com.github.codestorm'
version = '1.0'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'com.github.almasb:fxgl:21.1'
	implementation 'org.slf4j:slf4j-api:2.0.17'
	implementation 'ch.qos.logback:logback-classic:1.5.18'
	implementation("com.google.guava:guava:33.5.0-jre")
}

test {
	useJUnitPlatform()
}

javafx {
	version = "25"
	modules = [ 'javafx.base' ]
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		leadingSpacesToTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat().aosp().reflowLongStrings()
		formatAnnotations()
	}
}

// enable flexible constructors (preview feature)
tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += "--enable-preview"
}

tasks.withType(Test).configureEach {
	jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
	jvmArgs += "--enable-preview"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}
