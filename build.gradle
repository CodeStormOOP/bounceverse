plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id 'com.diffplug.spotless' version "8.0.0"
}

repositories {
	mavenCentral()
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
		vendor.set(JvmVendorSpec.ADOPTIUM)
	}
}

dependencies {
	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'com.github.almasb:fxgl:21.1'
	implementation 'com.google.guava:guava:33.5.0-jre'
	implementation 'com.moandjiezana.toml:toml4j:0.7.2'
	implementation 'com.google.guava:guava:33.5.0-jre'
}

application {
	mainClass = 'com.github.codestorm.bounceverse.Bounceverse'
	applicationDefaultJvmArgs = [
			"--enable-native-access=javafx.graphics",
			"--enable-native-access=javafx.media",
			'-Dprism.forceGPU=true',
			'-Dmedia.gpu.decoder=true'
	]
}

defaultTasks('run')

tasks.register('release', Jar) {
	group = 'build'
	description = "Test this project and assemble a release JAR file."

	archiveBaseName.set(project.name)
	archiveVersion.set(project.version)
	destinationDirectory.set(file("./out"))

	dependsOn test
	dependsOn configurations.runtimeClasspath
	from sourceSets.main.output
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
	useJUnitPlatform()
}

javafx {
	version = "25"
	modules = ['javafx.controls', 'javafx.media']
}

spotless {
    enforceCheck = false

    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        target 'src/**/*.java'
        targetExclude 'src/**/libs/**/*';

        googleJavaFormat('1.17.0')
                .aosp()
                .reorderImports(true)
                .reflowLongStrings(true)
                .formatJavadoc(true)
        formatAnnotations()
        removeUnusedImports()
    }
}
